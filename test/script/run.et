module run

asset scripts:
    path: ../asset/run

asset scripts_success:
    path: ../asset/run-success

asset scripts_fail:
    path: ../asset/run-fail


test TrivialRun:
    spawn as p 
    with p:
        send "load ${scripts.path}/trivial.et"
        expect /load-done/

        send "run AlwaysSucceeds"
        local:
            expect /(run-.*)/ capture done
            guard (done == "run-done")

        send "run AlwaysFails"
        local:
            expect /match-fail .*/
            expect /(run-.*)/ capture done
            guard (done == "run-failed")


test SimpleRun:
    let should_succeed = [ "bool", "command-ignore" ]
    let should_fail = [ "bool" ]
    spawn as p

    with p:
        for file in should_succeed:
            send "load ${scripts_success.path}/$file.et"
            local:
                expect /(load-.*)/ capture done
                guard (done == "load-done")
            flush

            send "run Test"
            local:
                expect /(run-.*)/ capture done
                guard (done == "run-done")
            flush

        for file in should_fail:
            send "load ${scripts_fail.path}/$file.et"
            local:
                expect /(load-.*)/ capture done
                guard (done == "load-done")
            flush

            send "run Test"
            local:
                expect /(run-.*)/ capture done
                guard (done == "run-failed")
            flush


test RunConfig:
    node n
    local:
        shell on n:
            cp ${scripts.path}/erebos-tester.yaml .
            mkdir tools
            cp ${scripts.path}/tools/echo.sh ./tools/tool
            mkdir scripts
            cp ${scripts.path}/trivial.et ./scripts/
            cp ${scripts.path}/echo.et ./scripts/

    spawn as p on n

    with p:
        send "load-config"
        expect /load-config-done/
        send "run-all"
        expect /run-test-result AlwaysSucceeds done/
        expect /run-test-result AlwaysFails failed/
        expect /child-stdin p abcdef/
        expect /child-stdout p abcdef/
        expect /match p abcdef/
        expect /run-test-result ExpectEcho done/
        expect /run-all-done/


test GetSysInfo:
    node n
    local:
        shell on n:
            cp ${scripts.path}/erebos-tester.yaml .
            mkdir tools
            cp ${scripts.path}/tools/sysinfo.sh ./tools/tool
            mkdir scripts
            cp ${scripts.path}/sysinfo.et ./scripts/

    spawn as p on n

    with p:
        send "load-config"
        expect /load-config-done/
        send "run SysInfo"
        expect /run-done/


test CallStack:
    spawn as p
    with p:
        send "load ${scripts.path}/callstack.et"
        expect /load-done/

        send "run AG"
        expect /match-fail guard failed/
        expect /match-fail-line .*\/callstack.et:3:5: .*/
        expect /match-fail-var x 1/
        local:
            expect /(match-fail-.*)/ capture done
            guard (done == "match-fail-done")
        local:
            expect /(run-.*)/ capture done
            guard (done == "run-failed")
        flush

        send "run AE"
        expect /match-fail expect failed/
        expect /match-fail-line .*\/callstack.et:8:5: .*/
        expect /match-fail-var x 2/
        local:
            expect /(match-fail-.*)/ capture done
            guard (done == "match-fail-done")
        local:
            expect /(run-.*)/ capture done
            guard (done == "run-failed")
        flush

        send "run BG"
        expect /match-fail guard failed/
        expect /match-fail-line .*\/callstack.et:12:5: .*/
        expect /match-fail-var x 1/
        expect /match-fail-line .*\/callstack.et:15:5: .*/
        local:
            expect /(match-fail-.*)/ capture done
            guard (done == "match-fail-done")
        local:
            expect /(run-.*)/ capture done
            guard (done == "run-failed")
        flush

        send "run CG"
        expect /match-fail guard failed/
        expect /match-fail-line .*\/callstack.et:19:5: .*/
        expect /match-fail-var x 3/
        expect /match-fail-var y 2/
        expect /match-fail-line .*\/callstack.et:23:5: .*/
        expect /match-fail-var z 3/
        local:
            expect /(match-fail-.*)/ capture done
            guard (done == "match-fail-done")
        local:
            expect /(run-.*)/ capture done
            guard (done == "run-failed")
        flush

        send "run BE"
        expect /match-fail expect failed/
        expect /match-fail-line .*\/callstack.et:27:5: .*/
        expect /match-fail-var x 1/
        expect /match-fail-line .*\/callstack.et:31:5: .*/
        expect /match-fail-var p <process:p#[0-9]+>/
        local:
            expect /(match-fail-.*)/ capture done
            guard (done == "match-fail-done")
        local:
            expect /(run-.*)/ capture done
            guard (done == "run-failed")
        flush

        send "run CE"
        expect /match-fail expect failed/
        expect /match-fail-line .*\/callstack.et:36:5: .*/
        expect /match-fail-var x 3/
        expect /match-fail-var y 2/
        expect /match-fail-line .*\/callstack.et:41:5: .*/
        expect /match-fail-var p <process:p#[0-9]+>/
        expect /match-fail-var z 3/
        local:
            expect /(match-fail-.*)/ capture done
            guard (done == "match-fail-done")
        local:
            expect /(run-.*)/ capture done
            guard (done == "run-failed")
        flush
